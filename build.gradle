// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.8.0'

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:8.0.2')
        classpath("com.google.gms:google-services:4.3.15")
        classpath('com.google.firebase:firebase-crashlytics-gradle:2.9.5')
        classpath('org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22')
        classpath('com.google.dagger:hilt-android-gradle-plugin:2.46.1')
        classpath("androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3")
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.6'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.github.plnice.canidropjetifier" version "0.5"
    id "io.gitlab.arturbosch.detekt" version "1.16.0-RC1"
    id "org.jlleitschuh.gradle.ktlint" version "10.1.0"
    id 'com.google.dagger.hilt.android' version '2.46.1' apply false
}

detekt {
    input = files(rootProject.rootDir)
    failFast = true
    buildUponDefaultConfig = true
    config = files("$projectDir/config/detekt.yml")
    autoCorrect = true

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    exclude("**/build/**")
}


dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.16.0-RC1"
}

tasks.detekt.jvmTarget = "1.8"

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url { "https://jitpack.io" }
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 33
                buildToolsVersion = "29.0.3"

                defaultConfig {
                    minSdkVersion(26)
                    targetSdkVersion(33)
                    versionCode = System.getenv("BITRISE_BUILD_NUMBER")?.toInteger() ?: 20
                    versionName = "1.15"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_17
                    targetCompatibility = JavaVersion.VERSION_17
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_17.toString()
                }

                buildTypes {
                    release {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                buildFeatures.viewBinding = true

                packagingOptions {
                    exclude 'META-INF/AL2.0'
                    exclude 'META-INF/LGPL2.1'
                    exclude 'META-INF/LICENSE.md'
                    exclude 'META-INF/LICENSE-notice.md'
                    exclude 'build-data.properties'
                }
            }
        }
    }
}