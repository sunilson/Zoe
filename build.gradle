// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.4.10'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:4.1.2')
        classpath("com.google.gms:google-services:4.3.5")
        classpath('com.google.firebase:firebase-crashlytics-gradle:2.5.0')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.30")
        classpath("com.google.dagger:hilt-android-gradle-plugin:2.31.2-alpha")
        classpath("androidx.navigation:navigation-safe-args-gradle-plugin:2.3.3")
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.2'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.github.plnice.canidropjetifier" version "0.5"
    id "io.gitlab.arturbosch.detekt" version "1.16.0-RC1"
}

detekt {
    input = files(rootProject.rootDir)
    failFast = true
    buildUponDefaultConfig = true
    config = files("$projectDir/config/detekt.yml")
    autoCorrect = true

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    exclude("**/build/**")
}


dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.16.0-RC1"
}

tasks.detekt.jvmTarget = "1.8"

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url { "https://jitpack.io" }
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 30
                buildToolsVersion = "29.0.3"

                defaultConfig {
                    minSdkVersion(26)
                    targetSdkVersion(30)
                    versionCode = System.getenv("BITRISE_BUILD_NUMBER")?.toInteger() ?: 20
                    versionName = "1.15"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                }

                buildTypes {
                    release {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                buildFeatures.viewBinding = true

                packagingOptions {
                    exclude 'META-INF/AL2.0'
                    exclude 'META-INF/LGPL2.1'
                    exclude 'META-INF/LICENSE.md'
                    exclude 'META-INF/LICENSE-notice.md'
                    exclude 'build-data.properties'
                }
            }
        }
    }
}