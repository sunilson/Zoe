// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.4'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.0.1")
        classpath("com.google.gms:google-services:4.3.4")
        classpath('com.google.firebase:firebase-crashlytics-gradle:2.3.0')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10")
        classpath("com.google.dagger:hilt-android-gradle-plugin:2.28-alpha")
        classpath("androidx.navigation:navigation-safe-args-gradle-plugin:2.3.0")
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url { "https://jitpack.io" }
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 30
                buildToolsVersion = "29.0.3"

                defaultConfig {
                    minSdkVersion(26)
                    targetSdkVersion(30)
                    versionCode = System.getenv("BITRISE_BUILD_NUMBER")?.toInteger() ?: 20
                    versionName = "1.15"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                }

                packagingOptions {
                    exclude 'META-INF/AL2.0'
                    exclude 'META-INF/LGPL2.1'
                    exclude 'META-INF/LICENSE.md'
                    exclude 'META-INF/LICENSE-notice.md'
                }
            }
        }
    }
}